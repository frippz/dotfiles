# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

setopt extendedglob

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load system files first (for environment variables like XDG_*)
for file in ${(M)${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/system/*.zsh}
do
  source $file
done

# load everything else but the path, completion, and system files
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/system/*.zsh}
do
  source $file
done

# Autocomplete
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files
